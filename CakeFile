fs         = require 'fs'
{exec}     = require 'child_process'

appFiles  = [
  'coffeescript/animal.coffee' # put your files that must be loaded in first into the array
  'coffeescript/implementation.coffee'
]

task 'coffeeFiles', 'how much coffee you got?!', ->
  traverseFileSystem = (currentPath) ->
      files = fs.readdirSync currentPath
      for file in files
        do (file) ->
          currentFile = currentPath + '/' + file
          stats = fs.statSync(currentFile)
          if stats.isFile() and currentFile.indexOf('.coffee') > 1 and appFiles.join('=').indexOf("#{currentFile}=") < 0
            appFiles.push currentFile
          else if stats.isDirectory()
            traverseFileSystem currentFile

  traverseFileSystem 'coffeescript'
  console.log "#{appFiles.length} coffee files found."
  return appFiles

task 'watch', 'Watch prod source files and build changes', ->
    invoke 'build'
    console.log "Watching for changes in coffeescript"

    for file in appFiles then do (file) ->
        fs.watchFile file, (curr, prev) ->
            if +curr.mtime isnt +prev.mtime
                console.log "Saw change in #{file}"
                invoke 'build'

task 'build', 'Build single application file from source files', ->
  invoke 'coffeeFiles'
  appContents = new Array remaining = appFiles.length
  for file, index in appFiles then do (file, index) ->
    fs.readFile file, 'utf8', (err, fileContents) ->
      throw err if err
      appContents[index] = fileContents
      process() if --remaining is 0
  process = ->
    fs.writeFile 'javascript/app.coffee', appContents.join('\n\n'), 'utf8', (err) ->
      throw err if err
      exec 'coffee --compile javascript/app.coffee', (err, stdout, stderr) ->
        if err
          console.log 'Error compiling coffee file.'
        else
          fs.unlink 'javascript/app.coffee', (err) ->
            if err
              console.log 'Couldn\'t delete the app.coffee file/'
            console.log 'Done building coffee file.'